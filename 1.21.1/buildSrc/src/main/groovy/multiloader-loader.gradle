//file:noinspection GrUnresolvedAccess
plugins {
    id 'multiloader-common'
}

configurations {
    commonJava {
        canBeResolved = true
    }
    commonResources {
        canBeResolved = true
    }
    generatedResources {
        canBeResolved = true
    }
}

dependencies {
    compileOnly(project(':common'))
    commonJava project(path: ':common', configuration: 'commonJava')
    commonResources project(path: ':common', configuration: 'commonResources')
    generatedResources project(path: ':common', configuration: 'generatedResources')
}

tasks.named('compileJava', JavaCompile) {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

processResources {
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
    dependsOn(configurations.generatedResources) exclude(".cache/**")
    from(configurations.generatedResources) exclude(".cache/**")
}

tasks.named('javadoc', Javadoc).configure {
    dependsOn(configurations.commonJava)
    source(configurations.commonJava)
}

tasks.named('sourcesJar', Jar) {
    dependsOn(configurations.commonJava)
    from(configurations.commonJava)
    dependsOn(configurations.commonResources)
    from(configurations.commonResources)
    dependsOn(configurations.generatedResources) exclude(".cache/**")
    from(configurations.generatedResources) exclude(".cache/**")
}
